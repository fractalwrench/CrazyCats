apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "com.neenbedankt.android-apt"
apply plugin: "me.tatarka.retrolambda"

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.fractalwrench.crazycats"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0.0" // semver
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            // TODO would enable proguard + resource shrinking in a production app
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        mock {
            applicationIdSuffix ".demo"
        }
        live {
        }
    }
    packagingOptions {
        exclude "META-INF/services/javax.annotation.processing.Processor"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    // Support dependencies
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:support-v4:$supportLibraryVersion"

    // Butterknife
    compile "com.jakewharton:butterknife:$butterknifeVersion"
    apt "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    // Picasso
    compile "com.squareup.picasso:picasso:$picassoVersion"

    // PhotoView
    compile "com.github.chrisbanes:PhotoView:$photoViewVersion"

    // Dagger dependencies
    apt "com.google.dagger:dagger-compiler:$daggerVersion"
    provided "org.glassfish:javax.annotation:$daggerJavaxVersion"
    compile "com.google.dagger:dagger:$daggerVersion"

    // RxJava
    compile "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    compile "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    compile "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:$rxRetrofitAdapterVersion"

    // Retrofit
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttpLoggerVersion"

    // LeakCanary
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    // Testing Dependencies
    testCompile "junit:junit:$junitVersion"
    androidTestCompile "com.android.support.test:runner:$runnerVersion"
    androidTestCompile "com.android.support.test:rules:$runnerVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestCompile "com.android.support:support-annotations:$supportLibraryVersion"

    // exclude clashing dependencies for espresso
    androidTestCompile("com.android.support.test" +
            ".espresso:espresso-contrib:$espressoContribVersion") {
        exclude group: "com.android.support", module: "appcompat"
        exclude group: "com.android.support", module: "support-v4"
        exclude module: "recyclerview-v7"
    }
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

}
repositories {
    mavenCentral()
}
